@APPTITLE "ShutdownGUI"
@APPDESCRIPTION "Shutdown GUI countdown"
@APPAUTHOR "Enrique Mecklenburg Serkovic"
@APPVERSION "$VER: 0.4 (24.05.21)"

@REQUIRE "RapaGUI", {Link = True}
@DISPLAY {Width = 320, Height = 200, FillStyle = #FILLCOLOR, Color = #GRAY}

timeLeft$ = "10"										;Countdown start

Function p_EventFunc(msg)
	Switch msg.Class
		Case "Button":									;this is the only class for events, but just in case...
			Switch msg.Attribute
				Case "Pressed":
					Switch msg.ID						;check which button was pressed
						Case "btn_Ok":					;"Ok" button was pressed
							Execute("shutdown", "-h")	;Execute shutdown command with "-h" arg
							End()						;End program
						Case "btn_Cancel":				;"Cancel" button was pressed
							End()						;End program and cancel shutdown
					EndSwitch
			EndSwitch
	EndSwitch				
EndFunction

/*******************
 *  Main function  *
 *******************/
Function p_CountDown()
	timeLeft$ = timeLeft$ - 1							;Reduce countdown time
	SetDisplayAttributes({Color = #WHITE})				;Set display to white to clear old time
	SetDisplayAttributes({Color = #GRAY})				;Reset display to gray
	TextOut(#CENTER, #CENTER, timeLeft$)				;Display new time

	If timeLeft$ = 0									;If countdown ends...
		Execute("shutdown", "-h")						;Execute shutdown command with "-h" arg
		End()											;End program
	EndIf
EndFunction


InstallEventHandler({RapaGUI = p_EventFunc})

moai.CreateApp(											;Create GUI from file
[[<?xml version="1.0" encoding="utf-8"?>
<application>
	<window title="ShutdownGUI">
		<vgroup>
			<label align = "Left">Raspberry will shutdown in:</label>
			<hollywood display="1"/>
			<hgroup>
				<button id = "btn_Ok">Ok</button>
				<button id = "btn_Cancel">Cancel</button>
			</hgroup>
		</vgroup>
	</window>
</application>
]]
)

SetFont(#SANS,56)
SetFontStyle(#ANTIALIAS)
TextOut(#CENTER, #CENTER, timeLeft$)					;Display initial countdown time

SetInterval(1, p_Countdown, 1000)						;Run countdown with 1 second interval

Repeat
	WaitEvent()
Forever